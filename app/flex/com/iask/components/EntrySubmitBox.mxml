<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" label="新建条目   ">
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import com.iask.model.Course;
			import com.iask.model.Entry;
			import com.iask.model.Keyword;
			import com.iask.model.IaskModelLocator;
			import com.iask.util.CairngormUtils;
			import com.iask.control.EventNames;
			import mx.validators.Validator;
			import mx.controls.Alert;
			import mx.managers.CursorManager;
			import mx.core.IFlexDisplayObject;
			import mx.core.Application;
			import mx.managers.PopUpManager;
			
			[Bindable]
			private var _model:IaskModelLocator = IaskModelLocator.getInstance();
			
			private var _illustration:String = "为什么要设定条目关键词？\n\n" + "类似于学术论文中关键词的作用，设置条目关键词可以提高用户通过关键词搜索条目的准确度。因为您是此条目的编辑者，您清楚此条目中涉及了哪些概念，因此您设定的条目关键词要比单纯采用词频统计法（计算机完成）设置的关键词精准。这些关键词也可看作为带权的标签（TAG）。\n\n\n" + "怎样设定条目关键词？\n\n" + "对在此条目中出现的重要概念，加入能够描述这些概念的词汇。例如：戴维南定理，二端网络，等等。\n\n\n" + "为什么要设定关键词相对条目的关联度？\n\n" + "有些条目可能包含相同的关键词，设置关键词相对条目的关联度（即权重）可为搜索排序提供有力依据。例如：有若干条目均包含“戴维南定理”这一关键词，当用户输入”戴维南定理“进行搜索时，他可能更想知道这一定理的定义。因此，在解释戴维南定理的定义的条目中，“戴维南定理”这一关键词的关联度就应当设为最大值（100）；在有关戴维南定理的举例应用条目中，此关键词仍应设为比较大 ，但应比100小；而在其他涉及但并不是专门讲解此定理的条目中，“戴维南定理”这一关键词的关联度应设为较小。\n\n\n" + "怎样设定关键词相对条目的关联度？\n\n" + "您可结合上一问的例子以及该关键词在问题库出现的频率来综合进行关键词关联度的设定：若该关键词代表的概念是本条目所阐述的主要概念，并且在此课程的问题库中阐述此概念的条目较少，则该关键词相对本条目的关联度应设为较大；反之则应设为较小。";
			
			private function selectKeywordTIAll():void
			{
				keywordTI.selectionBeginIndex = 0;
				keywordTI.selectionEndIndex = keywordTI.text.length;
			}
			
			private function selectQuestionTIAll():void
			{
				questionTI.selectionBeginIndex = 0;
				questionTI.selectionEndIndex = questionTI.text.length;
			}
			
			private function handleKeywordsDGChange():void
			{
				keywordTI.text = keywordsDG.selectedItem.value;
			}
			
			private function handleCourseSelectCBChange():void
			{
				_model.selectedCourse = Course(courseSelectCB.selectedItem);
				if (courseSelectCB.selectedItem.keywords == null)
				{
					CairngormUtils.dispatchEvent(EventNames.LIST_COURSE_KEYWORDS, courseSelectCB.selectedItem);
				}
			}
			
			private function getRowNum(oItem:Object,iCol:int):String
    		{
    			var index:int = (keywordsDG.dataProvider as ArrayCollection).getItemIndex(oItem) + 1;
    			return index.toString();
    		}
			
			private function showIllustration():void
			{
				var detailWindow:IFlexDisplayObject = PopUpManager.createPopUp(Application.application.mainStack, ViewDetailBox, true);
    			var detailComponent:ViewDetailBox = ViewDetailBox(detailWindow);
    			detailComponent.width = 715;
    			detailComponent.height = 480;
    			PopUpManager.centerPopUp(detailWindow);
    			detailComponent.title = "关于条目关键词和关联度的说明";
    			detailComponent.detailTI.text = _illustration;
			}
			
			private function validateAndSubmit():void
			{	
				var results:Array = Validator.validateAll(
				[keywordValidator, questionValidator, answerValidator]);
				if (results.length > 0)
				{
					Alert.show("请改正表单上用红色线框标出的错误。", "创建条目失败");
					return;
				}
				if (courseSelectCB.selectedItem == null)
				{
					Alert.show("请选择本条目属于的课程。", "创建条目失败");
					return;
				}
				if (relevanceSlider.value == 0)
				{
					Alert.show("关键词相对于本条目的关联度不能为0。", "创建条目失败");
					return;
				} 
				var entry:Entry = new Entry(questionTI.text, answerTI.htmlText, Course(courseSelectCB.selectedItem), 
					"", _model.user.officialId, _model.user.lastName, _model.user.firstName);
				var keyword:Keyword = new Keyword(keywordTI.text.toLowerCase(), relevanceSlider.value, entry);
				
				CairngormUtils.dispatchEvent(EventNames.CREATE_ENTRY, entry);
			}
		]]>
	</mx:Script>
	<mx:StringValidator id="keywordValidator" source="{keywordTI}" property="text"
   		required="true" minLength="1" maxLength="40" />
   	<mx:StringValidator id="questionValidator" source="{questionTI}" property="text"
   		required="true" minLength="1" maxLength="120" />
   	<mx:StringValidator id="answerValidator" source="{answerTI}" property="text"
   		required="true" minLength="1"/>
	<mx:HDividedBox width="100%" height="100%">
		<mx:DataGrid height="100%" width="135" id="keywordsDG" change="handleKeywordsDGChange()"
			dataProvider="{courseSelectCB.selectedItem.keywords}" rowHeight="24">
			<mx:columns>
				<mx:DataGridColumn headerText="此课程所有关键词" dataField="value" width="128"/>
			</mx:columns>
		</mx:DataGrid>
		<mx:VBox width="100%" height="100%">
			<mx:HBox width="100%" verticalAlign="middle">
				<mx:Text text="条目所属课程："/>
				<mx:ComboBox dataProvider="{_model.courses}" labelField="name" prompt="请选择本条目所属的课程..."
					width="100%" id="courseSelectCB" change="handleCourseSelectCBChange()" rowCount="20"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Text text="标题："/>
				<mx:TextInput id="questionTI" width="100%" focusIn="selectQuestionTIAll()"/>
			</mx:HBox>
			<mx:RichTextEditor width="100%" height="100%" id="answerTI" title="解答"/>
			<mx:HBox width="100%"  verticalAlign="middle">
				<mx:Text text="条目关键词："/>
				<mx:TextInput id="keywordTI" focusIn="selectKeywordTIAll()"/>
				<mx:Button label=" ？" color="#ff9900" width="25" height="25" click="showIllustration()" 
					toolTip="查看关键词和关联度帮助" fontSize="15" fontWeight="bold"/>
				<mx:Text text="相对本条目关联度：{relevanceSlider.value}"/>
				<mx:HSlider id="relevanceSlider" maximum="100" snapInterval="1" tickInterval="25"
				    labels="[0, 25, 50, 75, 100]" width="100%"/>
			</mx:HBox>
			<mx:Button width="100%" label="提交" click="validateAndSubmit()"/>
		</mx:VBox>
	</mx:HDividedBox>
</mx:VBox>
